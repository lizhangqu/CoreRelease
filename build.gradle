buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:4.3.0'
        classpath 'nu.studer:gradle-plugindev-plugin:1.0.4'
        classpath 'org.codehaus.groovy:groovy-backports-compat23:2.4.6'
        classpath 'net.researchgate:gradle-release:2.3.5'
    }
}

apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'nu.studer.plugindev'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'net.researchgate.release'

group='net.researchgate'

dependencies {
    testCompile("org.spockframework:spock-core:$spockVersion") { exclude group: 'org.codehaus.groovy' }
    testCompile "junit:junit:$junitVersion"
    testCompile "org.eclipse.jgit:org.eclipse.jgit:$jgitVersion"
    testCompile "cglib:cglib-nodep:$cglibVersion"
}

plugindev {
    pluginImplementationClass 'net.researchgate.release.ReleasePlugin'
    pluginDescription 'gradle-release is a plugin for providing a Maven-like release process to project using Gradle.'
    pluginLicenses 'MIT'
    pluginTags 'gradle', 'plugin', 'release'
    authorId 'danez'
    authorName 'Daniel Tschinder'
    authorEmail 'daniel.tschinder@researchgate.net'
    projectUrl 'https://github.com/researchgate/gradle-release'
    projectInceptionYear '2011'
    done() // do not omit this
}

release {
    git {
        requireBranch = '(master|\\d+\\.\\d+)'
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? bintrayUser : ''
    key = project.hasProperty('bintrayApiKey') ? bintrayApiKey : ''
    pkg {
        repo = 'gradle-plugins'
        userOrg = 'researchgate'
        version {
            gpg {
                sign = false
            }
            mavenCentralSync {
                sync = false
                user = project.hasProperty('sonatypeUser') ? sonatypeUser : ''
                password = project.hasProperty('sonatypePassword') ? sonatypePassword : ''
            }
        }
    }
    publish = false
}

artifactory {
    contextUrl = 'https://oss.jfrog.org'
    publish {
        repository {
            repoKey = 'oss-snapshot-local' //The Artifactory repository key to publish to
            username = project.hasProperty('bintrayUser') ? bintrayUser : ''
            password = project.hasProperty('bintrayApiKey') ? bintrayApiKey : ''
        }
        defaults {
            publications 'plugin' // That is how it is named in plugindev plugin
            properties = ['bintray.repo': 'gradle-plugins', 'bintray.package': 'gradle-release', 'bintray.version': version.toString()]
        }
    }
    resolve {
        repoKey = 'jcenter'
    }
}


apply plugin: 'maven'
def readPropertyFromLocalProperties(String key) {
    Properties properties = new Properties()
    try {
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
    } catch (Exception e) {
        println("load local properties failed msg:${e.message}")
    }
    return properties.getProperty(key)
}

def getReleaseRepositoryUrl() {
    return hasProperty('RELEASE_REPOSITORY_URL') ? RELEASE_REPOSITORY_URL : readPropertyFromLocalProperties('RELEASE_REPOSITORY_URL')
}

def getSnapshotRepositoryUrl() {
    return hasProperty('SNAPSHOT_REPOSITORY_URL') ? SNAPSHOT_REPOSITORY_URL : readPropertyFromLocalProperties('SNAPSHOT_REPOSITORY_URL')
}

def getReleaseRepositoryUsername() {
    return hasProperty('RELEASE_REPOSITORY_USERNAME') ? RELEASE_REPOSITORY_USERNAME : readPropertyFromLocalProperties('RELEASE_REPOSITORY_USERNAME')
}

def getReleaseRepositoryPassword() {
    return hasProperty('RELEASE_REPOSITORY_PASSWORD') ? RELEASE_REPOSITORY_PASSWORD : readPropertyFromLocalProperties('RELEASE_REPOSITORY_PASSWORD')
}

def getSnapshotRepositoryUsername() {
    return hasProperty('SNAPSHOT_REPOSITORY_USERNAME') ? SNAPSHOT_REPOSITORY_USERNAME : readPropertyFromLocalProperties('SNAPSHOT_REPOSITORY_USERNAME')
}

def getSnapshotRepositoryPassword() {
    return hasProperty('SNAPSHOT_REPOSITORY_PASSWORD') ? SNAPSHOT_REPOSITORY_PASSWORD : readPropertyFromLocalProperties('SNAPSHOT_REPOSITORY_PASSWORD')
}

def getPomGroupId() {
    return hasProperty('PROJECT_POM_GROUP') ? PROJECT_POM_GROUP : readPropertyFromLocalProperties('PROJECT_POM_GROUP')
}

def getPomArtifactId() {
    return hasProperty('PROJECT_POM_ARTIFACT_ID') ? PROJECT_POM_ARTIFACT_ID : readPropertyFromLocalProperties('PROJECT_POM_ARTIFACT_ID')
}

def getPomVersion() {
    return hasProperty('PROJECT_POM_VERSION') ? PROJECT_POM_VERSION : readPropertyFromLocalProperties('PROJECT_POM_VERSION')
}

uploadArchives {
    repositories {
        mavenDeployer {
            pom.groupId = getPomGroupId()
            pom.artifactId = getPomArtifactId()
            pom.version = getPomVersion()

            repository(url: getReleaseRepositoryUrl()) {
                authentication(userName: getReleaseRepositoryUsername(), password: getReleaseRepositoryPassword())
            }
            snapshotRepository(url: getSnapshotRepositoryUrl()) {
                authentication(userName: getSnapshotRepositoryUsername(), password: getSnapshotRepositoryPassword())
            }
        }
    }
}


afterReleaseBuild.dependsOn uploadArchives

wrapper.gradleVersion = '1.12'

